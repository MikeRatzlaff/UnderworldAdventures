dnl
dnl configure.in for Underworld Adventures
dnl
dnl $Id$
dnl


dnl initial stuff
AC_PREREQ(2.53)
AC_INIT(../uwadv.cpp)

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE(uwadv,0.0.1)
AM_CONFIG_HEADER(config.h)


dnl initial variable values
CFLAGS="$CFLAGS -O2"
CXXFLAGS="$CXXFLAGS -O2"


dnl find and test the compiler
AC_PROG_CC
AC_PROG_CPP
AC_C_INLINE
AC_C_CONST

AC_PROG_CXX
AC_LANG_CPLUSPLUS


dnl other needed programs
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_RANLIB


dnl X config
AC_PATH_X
AC_PATH_XTRA
CFLAGS="$CFLAGS $X_CFLAGS"
CXXFLAGS="$CXXFLAGS $X_CFLAGS"
LIBS="$LIBS $X_LIBS"


dnl check standard C header
AC_HEADER_STDC


dnl checking for --enable-debug
AC_MSG_CHECKING(for debug options)

AC_ARG_ENABLE(debug,
  [  --enable-debug          enable debugging (disables optimizations)],
  [ CXXFLAGS="-O -g -wall $CXXFLAGS"
    AC_MSG_RESULT(yes)
  ], AC_MSG_RESULT(no))


dnl checking for SDL
AM_PATH_SDL(1.2.3,,AC_MSG_ERROR([
Underworld Adventures needs at least SDL 1.2.3 or higher installed.
SDL is available here: http://www.libsdl.org/
]))


dnl checking for working OpenGL
save_CPPFLAGS="$CPPFLAGS"
CPPFLAGS="$CXXFLAGS"
AC_CHECK_HEADER(GL/gl.h,,AC_MSG_ERROR([
OpenGL header not found. An OpenGL graphics library is required
to compile and run Underworld Adventures.
Check http://mesa3d.sourceforge.net/ for a good one.]))
CPPFLAGS="$save_CPPFLAGS"


dnl checking for FMOD
AC_CHECK_HEADER(fmod.h,
  AC_DEFINE(HAVE_FMOD_H,,[defined when the FMOD library is present]),
  AC_MSG_WARN([
FMOD not found. No midi playback available.
FMOD is available at http://www.fmod.org/]))


dnl checking for uwadv.cfg entries
UW1PATH=/usr/local/games/uw1/
AC_SUBST(UW1PATH)

UADATAPATH=../uadata/
AC_SUBST(UADATAPATH)


dnl produce output files
AC_CONFIG_FILES([../Makefile ../tools/Makefile uwadv.cfg])
AC_OUTPUT

